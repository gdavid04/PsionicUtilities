buildscript {
	repositories {
		maven { url = 'https://maven.minecraftforge.net' }
		maven { url = 'https://repo.spongepowered.org/maven' }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1+', changing: true
		classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

// version in gradle.properties
group = 'gdavid.psionicutilities'
archivesBaseName = 'psionicutilities'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

minecraft {
	mappings channel: 'official', version: '1.18.2'
	runs {
		client {
			workingDirectory project.file('run')
			mods {
				psionicutilities { source sourceSets.main }
			}
		}
	}
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
	maven { url "https://maven.blamejared.com/" }
}

dependencies {
	minecraft group: 'net.minecraftforge', name: 'forge', version: "1.18.2-40.1.19"
	annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

	compileOnly fg.deobf("vazkii.psi:Psi:${psi_version}")
	runtimeOnly fg.deobf("vazkii.psi:Psi:${psi_version}")

	runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")
}

mixin {
	add sourceSets.main, "psionicutilities.refmap.json"
	config 'psionicutilities.mixins.json'
}

jar {
	manifest {
		attributes([
			"Specification-Title": "psionicutilities",
			"Specification-Vendor": "gdavid",
			"Specification-Version": "1",
			"Implementation-Title": project.name,
			"Implementation-Version": "${version}",
			"Implementation-Vendor" :"gdavid",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			"MixinConfigs": "psionicutilities.mixins.json"
		])
	}
}

build.dependsOn('reobfJar')

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact jar
		}
	}
	repositories {
		maven {
			url "file:///${project.projectDir}/mcmodsrepo"
		}
	}
}
