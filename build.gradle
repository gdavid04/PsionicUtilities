buildscript {
	repositories {
		maven { url = 'https://files.minecraftforge.net/maven' }
		maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
		mavenCentral()
	}
	dependencies {
		classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
		classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
	}
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'

// version in gradle.properties
group = 'gdavid.psionicutilities'
archivesBaseName = 'psionicutilities'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
	mappings channel: "snapshot", version: "20200916-1.16.2"
	runs {
		client {
			workingDirectory project.file('run')
			property 'forge.logging.console.level', 'debug'
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
			arg "-mixin.config=psionicutilities.mixins.json"
			mods {
				psionicutilities { source sourceSets.main }
			}
		}
		server {
			workingDirectory project.file('run')
			property 'forge.logging.console.level', 'debug'
			property 'mixin.env.remapRefMap', 'true'
			property 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
			mods {
				psionicutilities { source sourceSets.main }
			}
		}
		data {
			workingDirectory project.file('run')
			property 'forge.logging.console.level', 'debug'
			args '--mod', 'psionicutilities', '--all',
				'--output', file('src/generated/resources/'),
				'--existing', file('src/main/resources/')
			mods {
				psionicutilities { source sourceSets.main }
			}
		}
	}
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
	maven { url "http://maven.blamejared.com/" }
}

dependencies {
	minecraft group: 'net.minecraftforge', name: 'forge', version: "1.16.5-36.2.26"
	annotationProcessor 'org.spongepowered:mixin:0.8:processor'

	compileOnly fg.deobf("vazkii.psi:Psi:${psi_version}")
	runtimeOnly fg.deobf("vazkii.psi:Psi:${psi_version}")

	runtimeOnly fg.deobf("vazkii.patchouli:Patchouli:${patchouli_version}")
}

mixin {
	add sourceSets.main, "psionicutilities.refmap.json"
}

jar {
	manifest {
		attributes([
			"Specification-Title": "psionicutilities",
			"Specification-Vendor": "gdavid",
			"Specification-Version": "1",
			"Implementation-Title": project.name,
			"Implementation-Version": "${version}",
			"Implementation-Vendor" :"gdavid",
			"Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
			"MixinConfigs": "psionicutilities.mixins.json"
		])
	}
}

build.dependsOn('reobfJar')

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact jar
		}
	}
	repositories {
		maven {
			url "file:///${project.projectDir}/mcmodsrepo"
		}
	}
}
